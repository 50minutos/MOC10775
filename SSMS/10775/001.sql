USE MASTER

IF EXISTS(SELECT * FROM SYS.DATABASES WHERE NAME = 'DB_OLTP')
BEGIN
	ALTER DATABASE DB_OLTP
	SET SINGLE_USER
	WITH ROLLBACK IMMEDIATE

	DROP DATABASE DB_OLTP
END

CREATE DATABASE DB_OLTP
GO

USE DB_OLTP

CREATE TABLE CLIENTE
(
	COD_CLIENTE INT IDENTITY PRIMARY KEY, 
	NOME_CLIENTE VARCHAR(50), 
	SEXO_CLIENTE CHAR(1)
)

CREATE TABLE FUNCIONARIO
(
	COD_FUNCIONARIO INT IDENTITY PRIMARY KEY, 
	NOME_FUNCIONARIO VARCHAR(50), 
	DATA_CONTRATACAO_FUNCIONARIO DATE 
)

CREATE TABLE PRODUTO
(
	COD_PRODUTO INT IDENTITY PRIMARY KEY, 
	NOME_PRODUTO VARCHAR(50), 
	PRECO_PRODUTO DEC(9, 2)
)

CREATE TABLE PEDIDO
(
	COD_PEDIDO INT IDENTITY PRIMARY KEY, 
	DATA_PEDIDO DATE, 
	COD_CLIENTE INT REFERENCES CLIENTE, 
	COD_FUNCIONARIO INT REFERENCES FUNCIONARIO
)

CREATE TABLE ITEM_PEDIDO
(
	COD_ITEM_PEDIDO INT IDENTITY PRIMARY KEY, 
	COD_PEDIDO INT REFERENCES PEDIDO, 
	COD_PRODUTO INT REFERENCES PRODUTO, 
	QTD_ITEM_PEDIDO INT, 
	PRECO_ITEM_PEDIDO DEC(9, 2)
)

INSERT CLIENTE 
VALUES ('ADÃO', 'M'), 
	('EVA', 'F')

INSERT FUNCIONARIO
VALUES ('CAIM', '20130204'), 
	('ABEL', '20130601')

INSERT PRODUTO 
VALUES ('MARTELO', 10.10), 
('MARRETA', 20.20), 
('SERROTE', 30.30), 
('MACHADO', 40.40)

INSERT PEDIDO
VALUES ('20120103', 1, 1), 
	('20120212', 2, 1), 
	('20130403', 1, 1), 
	('20130403', 1, 2), 
	('20130502', 1, 1), 
	('20130502', 2, 1)

INSERT ITEM_PEDIDO
VALUES (1, 1, 1, 10.10), 
	(1, 2, 1, 20.20), 
	(1, 3, 10, 30.30), 
	(2, 1, 1, 10.10), 
	(2, 3, 2, 30.30), 
	(3, 1, 1, 10.10), 
	(4, 4, 1, 40.40), 
	(5, 1, 1, 10.10), 
	(5, 2, 1, 20.20), 
	(5, 3, 1, 30.30), 
	(5, 4, 5, 40.40), 
	(6, 4, 100, 40.40)

-------------

USE MASTER

IF EXISTS(SELECT * FROM SYS.DATABASES WHERE NAME = 'DB_OLAP')
BEGIN
	ALTER DATABASE DB_OLAP
	SET SINGLE_USER
	WITH ROLLBACK IMMEDIATE

	DROP DATABASE DB_OLAP
END

CREATE DATABASE DB_OLAP
GO

USE DB_OLAP

CREATE TABLE DIMENSAO_CLIENTE --QUEM
(
	COD_CLIENTE INT IDENTITY PRIMARY KEY, 
	NOME_CLIENTE VARCHAR(50)	
)

CREATE TABLE DIMENSAO_FUNCIONARIO --QUEM
(
	COD_FUNCIONARIO INT IDENTITY PRIMARY KEY, 
	NOME_FUNCIONARIO VARCHAR(50)	
)

CREATE TABLE DIMENSAO_PRODUTO --O QUE
(
	COD_PRODUTO INT IDENTITY PRIMARY KEY,
	NOME_PRODUTO VARCHAR(50)
)

CREATE TABLE DIMENSAO_TEMPO --QUANDO
(
	DATA DATE PRIMARY KEY, 
	ANO INT, 
	MES INT, 
	DIA INT
)

CREATE TABLE FATO_VENDA
(
	COD_PEDIDO INT, 
	DATA_PEDIDO DATE REFERENCES DIMENSAO_TEMPO, 
	COD_CLIENTE INT REFERENCES DIMENSAO_CLIENTE, 
	COD_FUNCIONARIO INT REFERENCES DIMENSAO_FUNCIONARIO, 
	COD_PRODUTO INT REFERENCES DIMENSAO_PRODUTO, 
	PRECO_ITEM_PEDIDO DEC(9, 2), 
	QTD_ITEM_PEDIDO INT, 
	ANO_PEDIDO INT, 
	MES_PEDIDO INT, 
	DIA_PEDIDO INT
)

-------------

--USAR INTEGRATION SERVICES PARA IMPORTAR OS DADOS (ETL)

SET IDENTITY_INSERT DIMENSAO_CLIENTE ON 

INSERT DIMENSAO_CLIENTE 
(
	COD_CLIENTE, 
	NOME_CLIENTE
)
SELECT COD_CLIENTE, 
	NOME_CLIENTE 
FROM DB_OLTP..CLIENTE

SET IDENTITY_INSERT DIMENSAO_CLIENTE OFF

SET IDENTITY_INSERT DIMENSAO_FUNCIONARIO ON 

INSERT DIMENSAO_FUNCIONARIO
(
	COD_FUNCIONARIO, 
	NOME_FUNCIONARIO
)
SELECT COD_FUNCIONARIO, 
	NOME_FUNCIONARIO
FROM DB_OLTP..FUNCIONARIO

SET IDENTITY_INSERT DIMENSAO_FUNCIONARIO OFF

SET IDENTITY_INSERT DIMENSAO_PRODUTO ON 

INSERT DIMENSAO_PRODUTO
(
	COD_PRODUTO, 
	NOME_PRODUTO
)
SELECT COD_PRODUTO, 
	NOME_PRODUTO
FROM DB_OLTP..PRODUTO

SET IDENTITY_INSERT DIMENSAO_PRODUTO OFF

INSERT DIMENSAO_TEMPO
(
	DATA, 
	ANO, 
	MES, 
	DIA
)
SELECT DISTINCT DATA_PEDIDO, 
	YEAR(DATA_PEDIDO), 
	MONTH(DATA_PEDIDO), 
	DAY(DATA_PEDIDO)
FROM DB_OLTP..PEDIDO

INSERT FATO_VENDA
(
	COD_PEDIDO, 
	DATA_PEDIDO, 
	COD_CLIENTE, 
	COD_FUNCIONARIO, 
	COD_PRODUTO, 
	PRECO_ITEM_PEDIDO, 
	QTD_ITEM_PEDIDO, 
	ANO_PEDIDO, 
	MES_PEDIDO, 
	DIA_PEDIDO
)
SELECT P.COD_PEDIDO, 
	P.DATA_PEDIDO, 
	C.COD_CLIENTE, 
	F.COD_FUNCIONARIO, 
	X.COD_PRODUTO, 
	IP.PRECO_ITEM_PEDIDO, 
	IP.QTD_ITEM_PEDIDO,
	YEAR(P.DATA_PEDIDO), 
	MONTH(P.DATA_PEDIDO), 
	DAY(P.DATA_PEDIDO)
FROM DB_OLTP..PEDIDO P 
JOIN DB_OLTP..ITEM_PEDIDO IP
	ON P.COD_PEDIDO = IP.COD_PEDIDO
JOIN DB_OLTP..PRODUTO X
	ON X.COD_PRODUTO = IP.COD_PRODUTO
JOIN DB_OLTP..CLIENTE C 
	ON C.COD_CLIENTE = P.COD_CLIENTE
JOIN DB_OLTP..FUNCIONARIO F 
	ON F.COD_FUNCIONARIO = P.COD_FUNCIONARIO

-------------

--USAR O ANALYSIS SERVICES PARA CRIAR O "CUBO"

-------------

--F1 - HELP DO COMANDO SELECIONADO

-------------

--MOSTRAR PROFILER

--MOSTRAR TUNING ADVISOR

--MOSTRAR CONFIGURATION MANAGER

--MOSTRAR POWERSHELL

USE DB_OLTP

SELECT *
FROM CLIENTE

EXEC SP_TABLES
